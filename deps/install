#==============================================================================
# README
#------------------------------------------------------------------------------
# This script assumes it is executed from CMAKE_SOURCE_DIR.
# In other words, from the root of the main project.
#==============================================================================

#------------------------------------------------------------------------------
# Configure the build type when compiling the external dependencies
#------------------------------------------------------------------------------
if(NOT DEFINED BUILD_TYPE)
    set(BUILD_TYPE Release)
endif()

#------------------------------------------------------------------------------
# Configure the path where the external dependencies are built
#------------------------------------------------------------------------------
if(NOT DEFINED BUILD_PATH)
    set(BUILD_PATH ${CMAKE_SOURCE_DIR}/deps/build)
endif()

message(STATUS "Using ${BUILD_PATH} as the path where the external dependencies are built.")

#------------------------------------------------------------------------------
# Configure the cmake generator to build/install the external dependencies
#------------------------------------------------------------------------------
if(DEFINED GENERATOR AND GENERATOR MATCHES "make")
    set(GENERATOR "Unix Makefiles")
endif()

if(DEFINED GENERATOR AND GENERATOR MATCHES "ninja")
    set(GENERATOR "Ninja")
endif()

if(NOT DEFINED GENERATOR)
    set(GENERATOR "Ninja") # Use Ninja by default, which is cross-platform
endif()

message(STATUS "Using ${GENERATOR} as the build tool.")

#------------------------------------------------------------------------------
# Configure the number of parallel jobs for the compilation of dependencies
#------------------------------------------------------------------------------
if(NOT DEFINED JOBS AND GENERATOR MATCHES "Ninja")
    set(JOBS "0")
endif()

if(NOT DEFINED JOBS AND GENERATOR MATCHES "Unix Makefiles")
    set(JOBS "")
endif()

message(STATUS "Using -j${JOBS} as instruction for parallel jobs for compiling the external dependencies.")

#------------------------------------------------------------------------------
# Create the build directory for the external dependencies
#------------------------------------------------------------------------------
execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory ${BUILD_PATH})

#------------------------------------------------------------------------------
# Configure the installation of the external dependencies
#------------------------------------------------------------------------------
execute_process(COMMAND
    ${CMAKE_COMMAND} -E chdir ${BUILD_PATH}
    ${CMAKE_COMMAND} -G ${GENERATOR} ${CMAKE_SOURCE_DIR}/deps
        -DCMAKE_BUILD_TYPE=${BUILD_TYPE}
        -DCMAKE_INSTALL_PREFIX=${BUILD_PATH}/install)

#------------------------------------------------------------------------------
# Build and install the external dependencies
#------------------------------------------------------------------------------
execute_process(COMMAND ${CMAKE_COMMAND} --build ${BUILD_PATH} -- -j ${JOBS})
